apply plugin: 'java'

defaultTasks 'clean', 'check'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

configurations.configureEach {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    url uri('https://oss.sonatype.org/content/repositories/snapshots/')
    mavenContent {
      snapshotsOnly()
    }
  }
}

compileJava {
  options.debugOptions.debugLevel = "source,lines,vars"
  options.fork = true
  options.compilerArgs << '-parameters'
}

sourceSets {
  main {
    java {srcDir "app"}
    resources {srcDirs "conf", "app"}
  }
  test {
    java {srcDir "test"}
    resources {srcDir "test"}
  }
}

buildscript {
  ext.replay_version = '2.3.1'
  ext.httpclient_version = '4.5.14'
  ext.junitVersion = '5.10.3'
}

dependencies {
  implementation("com.codeborne.replay:framework:${replay_version}")
  implementation("com.codeborne.replay:fastergt:${replay_version}")
  implementation("com.codeborne.replay:guice:${replay_version}")
  implementation("com.codeborne.replay:pdf:${replay_version}")
  implementation("com.codeborne.replay:javanet:${replay_version}")
  implementation("org.apache.httpcomponents:httpclient:${httpclient_version}")
  implementation("org.apache.httpcomponents:fluent-hc:${httpclient_version}")

  testImplementation('org.assertj:assertj-core:3.26.3')
  testImplementation('com.codeborne:selenide:7.4.1')
  testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
  testImplementation("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
  testImplementation('org.mockito:mockito-core:5.12.0')
  testImplementation('org.subethamail:subethasmtp:3.1.7') {transitive = false}
  testImplementation('com.github.tomakehurst:wiremock:3.0.1') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-client'
  }
  testImplementation('com.fasterxml.jackson.core:jackson-databind:2.17.2')
  testImplementation('com.codeborne:pdf-test:1.9.0')

  constraints {
    testImplementation("com.jayway.jsonpath:json-path:2.9.0") {because 'used by wiremock'}
    testImplementation("org.eclipse.jetty:jetty-bom:11.0.22") {because 'used by wiremock'}
  }
}

test {
  exclude 'ui/**'
}

tasks.register('uitest', Test) {
  include 'ui/**/*'
  systemProperties['selenide.headless'] = 'true'
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  systemProperties['file.encoding'] = "UTF-8"
  systemProperties['user.country'] = 'TR'
  systemProperties['user.language'] = 'tr'
  testLogging {
    events "passed", "skipped", "failed"
    showExceptions true
    exceptionFormat 'full'
  }
  outputs.upToDateWhen { false }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}
