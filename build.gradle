apply plugin: 'java'

defaultTasks 'clean', 'check'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

configurations.configureEach {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
  mavenCentral()
  mavenLocal()
}

compileJava {
  options.debugOptions.debugLevel = "source,lines,vars"
  options.fork = true
  options.compilerArgs << '-parameters'
}

sourceSets {
  main {
    java {srcDir "app"}
    resources {srcDirs "conf", "app"}
  }
  test {
    java {srcDir "test"}
    resources {srcDir "test"}
  }
}

buildscript {
  ext.replay_version = '2.7.1'
  ext.httpclient_version = '4.5.14'
  ext.junitVersion = '6.0.2'
}

dependencies {
  implementation("io.github.replay-framework:netty4:${replay_version}")
  implementation("io.github.replay-framework:framework:${replay_version}")
  implementation("io.github.replay-framework:fastergt:${replay_version}")
  implementation("io.github.replay-framework:guice:${replay_version}")
  implementation("io.github.replay-framework:pdf:${replay_version}")
  implementation("org.apache.httpcomponents:httpclient:${httpclient_version}")
  implementation("org.apache.httpcomponents:fluent-hc:${httpclient_version}")

  testImplementation('org.assertj:assertj-core:3.27.7')
  testImplementation('com.codeborne:selenide:7.14.0')
  testImplementation platform("org.junit:junit-bom:$junitVersion")
  testImplementation("org.junit.jupiter:junit-jupiter")
  testImplementation "org.junit.platform:junit-platform-engine"
  testImplementation "org.junit.platform:junit-platform-launcher"

  testImplementation('org.mockito:mockito-core:5.21.0')
  testImplementation('org.subethamail:subethasmtp:3.1.7') {transitive = false}
  testImplementation('org.wiremock:wiremock:3.13.2') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-client'
  }
  testImplementation('com.fasterxml.jackson.core:jackson-databind:2.21.0')
  testImplementation('com.codeborne:pdf-test:2.1.0')
}

test {
  exclude 'ui/**'
}

tasks.register('uitest', Test) {
  include 'ui/**/*'
  systemProperties['selenide.headless'] = 'true'
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  testClassesDirs = testing.suites.test.sources.output.classesDirs
  classpath = testing.suites.test.sources.runtimeClasspath
  systemProperties['file.encoding'] = "UTF-8"
  systemProperties['user.country'] = 'TR'
  systemProperties['user.language'] = 'tr'
  testLogging {
    events "passed", "skipped", "failed"
    showExceptions = true
    exceptionFormat = 'full'
  }
  outputs.upToDateWhen { false }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}
